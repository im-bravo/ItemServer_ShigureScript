// 挖掘 - Lv2铁
// 任意位置启动
//

tradeOresX = 28
tradeOresY = 6
tradeOrientation = 0
tradeOresName = "铁"
tradeIngotsName = "铁条"

isSaveInBank = 5
//信息提示("请问是要存银行还是卖店（存银行选是，卖店选否）", 36)

如果(isSaveInBank == 5)
	//银行密码如果没有就留空
	bankPwd = ""
	//信息输入("请输入银行密码（没有请留空）", "买菜的时雨", "")
	//根据你的银行空间大小,自行修改20 40 60 80.
	bankSize = 20
	//信息输入("请输入银行大小（［20-40-60-80］注意:错误的银行大小可能导致脚本出错！）","买菜的时雨", 20)
如果尾()

自动叠加道具(2, tradeIngotsName $ "|20")
自动叠加道具(2, tradeOresName $ "|40")
自动叠加道具(1)

isBack = 0

标记("挖矿")
循环( 1 )

	循环(人物详细信息(34) == "艾尔莎岛")
//		如果( isBack == 0 )
			循环( 获取信息记录("<back>","普通") != "" )
				//等待消除全部 <back>
				等待(100)
			循环尾()

			移动到(140,105)
			循环(人物详细信息(34) != "里谢里雅堡")
				对话NPC(1,2,"","","",2)
				tryCount = 0
				循环(tryCount < 20 && 人物详细信息(34) != "里谢里雅堡")
					等待(100)
					tryCount = tryCount + 1
				循环尾()
			循环尾()
//		如果尾()

//		如果(isBack != 0)
//		如果尾()
	循环尾()

	循环(人物详细信息(34) == "里谢里雅堡")
		如果(isBack == 0)
			如果(获取物品数量("矿山钥匙", "") < 1)
				转到("拿矿山钥匙")
			如果尾()

			自动寻路(17,53)
			循环(人物详细信息(34) != "法兰城")
				等待(100)
			循环尾()
		如果尾()

		如果(isBack != 0)
			//先去恢复
			自动寻路(34,89)
			护士恢复(1)
			自动寻路(17,53)
			循环(人物详细信息(34) != "法兰城")
				等待(100)
			循环尾()
		如果尾()
	循环尾()

	循环(人物详细信息(34) == "法兰城")
		如果(isBack == 0)
			curX = 人物详细信息(35)
			curY = 人物详细信息(36)
			flag = 0
			//从里堡出来
			如果(curX >= 120 && curX <= 141 && curY >= 80 && curY <= 95)
				flag = 1
				自动寻路(22,88)
				循环(人物详细信息(34) != "芙蕾雅")
					等待(100)
				循环尾()
			如果尾()
			//从银行出来
			如果(curX >= 230 && curX <= 240 && curY >= 106 && curY <= 116)
				flag = 3
				自动寻路(242,100)
				人物转身(2)
				循环(人物详细信息(34) != "市场三楼\S-\S修理专区\\z0")
					等待(100)
				循环尾()
				人物转身(0)
				循环(人物详细信息(34) != "法兰城")
					等待(100)
				循环尾()
				人物转身(0)
				循环(curX != 63 || curY != 79)
					curX = 人物详细信息(35)
					curY = 人物详细信息(36)
				循环尾()
				自动寻路(22,88)
				循环(人物详细信息(34) != "芙蕾雅")
					等待(100)
				循环尾()
			如果尾()
			//在法兰的其他地方
			如果(flag == 0)
				自动寻路(22,88)
				循环(人物详细信息(34) != "芙蕾雅")
					等待(100)
				循环尾()
			如果尾()
		如果尾()

		如果(isBack != 0)
			curX = 人物详细信息(35)
			curY = 人物详细信息(36)
			flag = 0
			//从里堡出来
			如果(curX >= 120 && curX <= 141 && curY >= 80 && curY <= 95)
				flag = 1
				//判断要不要换矿
				curOresCount = 获取物品数量(tradeOresName,"")
				如果(curOresCount > 19)
					发送聊天( tradeOresName $ ":" $ curOresCount, 2, 1 )
					自动寻路(106,61)
					循环(人物详细信息(34) != "米克尔工房\\z0")
						等待(100)
					循环尾()
				如果尾()

				如果(curOresCount < 20)
					//不需要换矿，直接去挖
					isBack = 0
				如果尾()
			如果尾()

			//从工房出来
			如果(curX >= 93 && curX <= 112 && curY >= 60 && curY <= 70)
				flag = 2
				//判断要不要存银行
				curIngotsCount = 获取物品数量(tradeIngotsName,"")
				如果(curIngotsCount > 19)
					发送聊天( tradeIngotsName $ ":" $ curIngotsCount, 2, 1 )
					自动寻路(63,79)
					人物转身(0)
					循环(curX != 242 || curY != 100)
						curX = 人物详细信息(35)
						curY = 人物详细信息(36)
					循环尾()
					自动寻路(238,111)
					循环(人物详细信息(34) != "银行\\z0")
						等待(100)
					循环尾()

				如果尾()

				如果(curIngotsCount < 20)
					//不需要存银行，直接去挖
					isBack = 0
				如果尾()
			如果尾()

		如果尾()
	循环尾()

	循环(人物详细信息(34) == "银行\\z0")
		如果(isBack == 0)
			自动寻路(2,13)
			循环(人物详细信息(34) != "法兰城")
				等待(100)
			循环尾()
		如果尾()

		如果(isBack != 0)
			自动寻路(11,8)
			打开银行(2,bankPwd)
			存取物品("存物品", tradeIngotsName, 20, "", bankSize)
			等待(1000)

			isBack = 0
		如果尾()
	循环尾()

	循环(人物详细信息(34) == "芙蕾雅")
		如果(isBack == 0)
			自动寻路(351,145)
			循环(人物详细信息(34) != "国营第24坑道\S地下1楼\\z12")
				等待(100)
			循环尾()
		如果尾()

		如果(isBack != 0)
		如果尾()
	循环尾()

	循环(人物详细信息(34) == "国营第24坑道\S地下1楼\\z12")
		如果(isBack == 0)
			自动寻路(22,22)
			循环(人物详细信息(34) != "国营第24坑道\S地下2楼\\z12")
				等待(100)
			循环尾()
		如果尾()

		如果(isBack != 0)
		如果尾()
	循环尾()

	循环(人物详细信息(34) == "国营第24坑道\S地下2楼\\z12")
		如果(isBack == 0)
			自动寻路(22,21)
			循环(人物详细信息(35) != 22 || 人物详细信息(36) != 19 )
				人物转身(0)
			循环尾()
			
			自动寻路(23,13)
			循环(人物详细信息(34) != "国营第24坑道\S\S地下3楼\\z12")
				等待(100)
			循环尾()
			
		如果尾()

		如果(isBack != 0)
		如果尾()
	循环尾()

	循环(人物详细信息(34) == "国营第24坑道\S\S地下3楼\\z12")
		如果(isBack == 0)
			自动寻路(29,3)
			循环(人物详细信息(34) != "国营第24坑道\S\S地下4楼\\z12")
				等待(100)
			循环尾()
			
		如果尾()

		如果(isBack != 0)
		如果尾()
	循环尾()

	循环(人物详细信息(34) == "国营第24坑道\S\S地下4楼\\z12")
		如果(isBack == 0)
			移动到(32,30)
			//开始挖矿
			myMana = 人物详细信息(4)
			如果(myMana != "")
				发送聊天("开始挖[" $ tradeOresName $ "]！",4,1)
				循环(人物详细信息(4) > 1 && 人物详细信息(33) < 20)
					高速采集("挖掘", 5100, "", "", "")
					如果(获取信息记录("<回补>","普通") != "")
						跳出循环()
					如果尾()
				循环尾()
			如果尾()
			如果(myMana == "")
				发送聊天("外挂可能出现问题，请重启外挂和游戏，但依然可继续采集！",6,1)
				高速采集("挖掘", 5200, 1, "", "")
			如果尾()
			//高速采集("挖掘", 5200, 1, "", "")
			//循环(人物详细信息(4) > 1 && 人物详细信息(33) < 20)
			//	高速采集("挖掘", 5100, "", "", "")

			//	如果( 获取信息记录("<back>","普通") != "" )
			//		跳出循环()
			//	如果尾()
			//循环尾()

			isBack = 1
			等待(1000)
			登出回城()
		如果尾()

		如果(isBack != 0)
		如果尾()
	循环尾()

	循环(人物详细信息(34) == "米克尔工房\\z0")
		如果(isBack == 0)
			自动寻路(26,23)
			移动到(26,24)
			循环(人物详细信息(34) != "法兰城")
				等待(100)
			循环尾()
		如果尾()

		如果(isBack != 0)
			自动寻路(tradeOresX,tradeOresY)
			//换矿
			交换物品(tradeOrientation,tradeOresName)
			等待(1000)
			自动寻路(26,23)
			移动到(26,24)
			循环(人物详细信息(34) != "法兰城")
				等待(100)
			循环尾()
		如果尾()
	循环尾()

循环尾()

标记("拿矿山钥匙")
循环( 1 )

	循环(人物详细信息(34) == "艾尔莎岛")
			移动到(140,105)
			循环(人物详细信息(34) != "里谢里雅堡")
				对话NPC(1,2,"","","",2)
				tryCount = 0
				循环(tryCount < 20 && 人物详细信息(34) != "里谢里雅堡")
					等待(100)
					tryCount = tryCount + 1
				循环尾()
			循环尾()
	循环尾()

	循环(人物详细信息(34) == "里谢里雅堡")
		如果(获取物品数量("给朋友的信") < 1)
			自动寻路(47,85)
			循环(人物详细信息(34) != "召唤之间\\z5")
				等待(100)
			循环尾()
		如果尾()

		如果(获取物品数量("给朋友的信") > 0)
			自动寻路(17,53)
			循环(人物详细信息(34) != "法兰城")
				等待(100)
			循环尾()
		如果尾()
	循环尾()

	循环(人物详细信息(34) == "法兰城")
		如果(获取物品数量("给朋友的信") < 1)
			登出回城()
		如果尾()

		如果(获取物品数量("给朋友的信") > 0)
			自动寻路(61,63)
			循环(人物详细信息(34) != "仓库内部\\z0")
				等待(100)
			循环尾()
		如果尾()
	循环尾()

	循环(人物详细信息(34) == "仓库内部\\z0")
		如果(获取物品数量("给朋友的信") < 1)
			登出回城()
		如果尾()
		如果(获取物品数量("给朋友的信") > 0)
			移动到(11,9)
			循环(获取物品数量("矿山钥匙") < 1)
				对话NPC(2,7,"","","","")
				等待(900)
			循环尾()
			登出回城()
			转到("挖矿")
		如果尾()
	循环尾()

	循环(人物详细信息(34) == "召唤之间\\z5")
		自动寻路(27,8)
		循环(人物详细信息(34) != "回廊\\z5")
			等待(100)
		循环尾()
	循环尾()

	循环(人物详细信息(34) == "回廊\\z5")
		自动寻路(23,19)
		循环(人物详细信息(34) != "灵堂\\z5")
			等待(100)
		循环尾()
	循环尾()

	循环(人物详细信息(34) == "灵堂\\z5")
		自动寻路(7,52)
		循环(人物详细信息(34) != "地下牢房\\z0")
			等待(100)
		循环尾()
	循环尾()

	循环(人物详细信息(34) == "地下牢房\\z0")
		自动寻路(31,20)
		循环(获取物品数量("给朋友的信") < 1)
			对话NPC(2, 7, "", "", "", 2)
			等待(900)
		循环尾()
		
		登出回城()
	循环尾()


循环尾()
