// 伐木 - Lv2枞
// 任意位置启动
//

//主产物
itemName = "枞"
itemStack = 40
//副产物
itemName2 = "印度轻木"
itemStack2 = 40

//目标地点
targetCoordStr = "451,163"

//银行密码如果没有就留空
bankPwd = ""
//信息输入("请输入银行密码（没有请留空）", "买菜的时雨", "")
//根据你的银行空间大小,自行修改20 40 60 80.
bankSize = 20
//信息输入("请输入银行大小（［20-40-60-80］注意:错误的银行大小可能导致脚本出错！）","买菜的时雨", 20)

//主产物保存到银行
isSaveToBank = 信息提示("请问[" $ itemName $ "]是要存银行还是卖店【存银行:是，卖店:否】", 36) == 5
如果(itemName2 != "")
	isSaveToBank2 = 信息提示("请问[" $ itemName2 $ "]是要存银行还是卖店【存银行:是，卖店:否】", 36) == 5
	自动叠加道具(2, itemName2 $ "|" $ itemStack2)
如果尾()

自动叠加道具(2, itemName $ "|" $ itemStack)
自动叠加道具(1)

//当前执行的动作【伐木，回补，存银行，卖木材】
curAction = 信息输入("执行动作【伐木，回补，存银行，卖木材】","执行动作","伐木")

循环( 1 )

	循环(人物详细信息(34) == "艾尔莎岛")
		//发送聊天("在[" $ 人物详细信息(34) $ "]执行[" $ curAction $ "]...", 4, 1)
		flag = 0
		如果(flag == 0)
			移动到(140,105)
			循环(人物详细信息(34) != "里谢里雅堡")
				对话NPC(1,2,"","","",2)
				tryCount = 0
				循环(tryCount < 20 && 人物详细信息(34) != "里谢里雅堡")
					等待(100)
					tryCount = tryCount + 1
				循环尾()
			循环尾()
		如果尾()

	循环尾()

	循环(人物详细信息(34) == "里谢里雅堡")
		//发送聊天("在[" $ 人物详细信息(34) $ "]执行[" $ curAction $ "]...", 4, 1)
		flag = 0
		如果(curAction == "伐木" || curAction == "卖木材" || curAction == "存银行")
			flag = 1
			自动寻路(41,98)
			循环(人物详细信息(34) != "法兰城")
				等待(100)
			循环尾()
		如果尾()

		如果(curAction == "回补")
			flag = 2
			curPet = 当前出战宠物()
			如果(人物详细信息(2) < 人物详细信息(3) || 人物详细信息(4) < 人物详细信息(5) || 宠物详细信息(curPet,2) < 宠物详细信息(curPet,3) || 宠物详细信息(curPet,4) < 宠物详细信息(curPet,5))
				//恢复血魔
				自动寻路(34,89)
				护士恢复(1)
			如果尾()

			curAction = "伐木"
			//判断是否要卖木材
			如果(isSaveToBank == 0 || isSaveToBank2 == 0)
				curAction = "卖木材"
			如果尾()
			如果(isSaveToBank != 0 && isSaveToBank2 != 0)
				curAction = "存银行"
			如果尾()

		如果尾()

		如果(flag == 0)
			发送聊天("动作[" $ curAction $ "]在[" $ 人物详细信息(34) $ "]是未定义的...", 6, 1)
		如果尾()
	循环尾()

	循环(人物详细信息(34) == "法兰城")
		//发送聊天("在[" $ 人物详细信息(34) $ "]执行[" $ curAction $ "]...", 4, 1)
		flag = 0
		如果(curAction == "伐木")
			flag = 1
			flag1 = 0
			curX = 人物详细信息(35)
			curY = 人物详细信息(36)
			//从银行出来
			如果(curX >= 230 && curX <= 240 && curY >= 106 && curY <= 116)
				flag1 = 3
				自动寻路(242,100)
				人物转身(2)
				循环(人物详细信息(34) != "市场三楼\S-\S修理专区\\z0")
					等待(100)
				循环尾()
				人物转身(0)
				循环(人物详细信息(34) != "法兰城")
					等待(100)
				循环尾()
				人物转身(0)
				循环(curX != 63 || curY != 79)
					curX = 人物详细信息(35)
					curY = 人物详细信息(36)
					等待(100)
				循环尾()

			如果尾()

			//在法兰的其他地方
			如果(flag1 == 0)
				自动寻路(22,88)
				循环(人物详细信息(34) != "芙蕾雅")
					等待(100)
				循环尾()
			如果尾()

		如果尾()

		如果(curAction == "卖木材")
			flag = 2
			自动寻路(155,122)
			saleItems = ""
			如果(isSaveToBank == 0)
				saleItems = itemName
			如果尾()
			如果(isSaveToBank2 == 0)
				如果(saleItems != "")
					saleItems = saleItems $ "|"
				如果尾()
				saleItems = saleItems $ itemName2
			如果尾()

			出售物品(2, saleItems)

			如果(isSaveToBank != 0 || isSaveToBank2 != 0)
				curAction = "存银行"
			如果尾()
			如果(isSaveToBank == 0 && isSaveToBank2 == 0)
				自动寻路(141,148)
				人物转身(0)
				curX = -1
				curY = -1
				循环(curX != 63 || curY != 79)
					curX = 人物详细信息(35)
					curY = 人物详细信息(36)
					等待(100)
				循环尾()
				curAction = "伐木"
			如果尾()
		如果尾()

		如果(curAction == "存银行")
			flag = 3
			自动寻路(141,148)
			人物转身(0)
			curX = -1
			curY = -1
			循环(curX != 63 || curY != 79)
				curX = 人物详细信息(35)
				curY = 人物详细信息(36)
				等待(100)
			循环尾()
			人物转身(0)
			循环(curX != 242 || curY != 100)
				curX = 人物详细信息(35)
				curY = 人物详细信息(36)
				等待(100)
			循环尾()
			自动寻路(238,111)
			循环(人物详细信息(34) != "银行\\z0")
				等待(100)
			循环尾()
		如果尾()

		如果(flag == 0)
			发送聊天("动作[" $ curAction $ "]在[" $ 人物详细信息(34) $ "]是未定义的...", 6, 1)
		如果尾()
	循环尾()

	循环(人物详细信息(34) == "银行\\z0")
		//发送聊天("在[" $ 人物详细信息(34) $ "]执行[" $ curAction $ "]...", 4, 1)
		flag = 0
		如果(curAction == "伐木")
			flag = 1
			自动寻路(2,13)
			循环(人物详细信息(34) != "法兰城")
				等待(100)
			循环尾()
		如果尾()

		如果(curAction == "存银行")
			flag = 2
			自动寻路(11,8)
			打开银行(2,bankPwd)
			如果(isSaveToBank != 0)
				存取物品("存物品", itemName, itemStack, "", bankSize)
			如果尾()
			如果(isSaveToBank2 != 0)
				存取物品("存物品", itemName2, itemStack2, "", bankSize)
			如果尾()
			等待(1000)

			curAction = "伐木"
		如果尾()

		如果(flag == 0)
			发送聊天("动作[" $ curAction $ "]在[" $ 人物详细信息(34) $ "]是未定义的...", 6, 1)
		如果尾()
	循环尾()

	循环(人物详细信息(34) == "芙蕾雅")
		//发送聊天("在[" $ 人物详细信息(34) $ "]执行[" $ curAction $ "]...", 4, 1)
		flag = 0
		如果(curAction == "伐木")
			flag = 1
			分割文本(targetCoord, targetCoordStr, ",")
			自动寻路(targetCoord.1,targetCoord.2)
			//开始挖矿
			myMana = 人物详细信息(4)
			如果(myMana != "")
				发送聊天("开始挖[" $ itemName $ "]！",4,1)
				循环(人物详细信息(4) > 1 && 人物详细信息(33) < 20)
					高速采集("伐木", 5100, "", "", "")
					如果( 获取信息记录("<回补>","普通") != "" )
						跳出循环()
					如果尾()
				循环尾()
			如果尾()
			如果(myMana == "")
				发送聊天("外挂可能出现问题，请重启外挂和游戏，但依然可继续采集！",6,1)
				高速采集("伐木", 5200, 1, "", "")
			如果尾()

			curAction = "回补"
		如果尾()

		如果(curAction == "回补")
			flag = 2
			等待(1000)
			登出回城()
			循环(人物详细信息(34) != "艾尔莎岛")
				等待(100)
			循环尾()
		如果尾()

		如果(flag == 0)
			发送聊天("动作[" $ curAction $ "]在[" $ 人物详细信息(34) $ "]是未定义的...", 6, 1)
		如果尾()
	循环尾()

循环尾()

