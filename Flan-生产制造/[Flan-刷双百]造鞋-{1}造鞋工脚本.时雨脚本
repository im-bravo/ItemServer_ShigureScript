// 此脚本是【造鞋刷双百】的主脚本，造鞋工使用
// 新城挖鹿皮需要【Lv1狩猎】而非【狩猎体验】


//是去挖鹿皮还是直接去造
如果(信息提示("是否去挖鹿皮？【是】去挖，【否】不去", 36) == 5)
	curAction = "挖鹿皮"
否则()
	//当前要干嘛（造鞋，挖鹿皮，回补，卖鞋，买布）
	curAction = 信息输入("造鞋，挖鹿皮，回补，卖鞋，买布","去干什么","造鞋")
如果尾()

// 是否打卡
isPunchTheClock = 信息提示("是否不打卡？【是】不打卡，【否】打卡", 36) != 5

//给配合者听
发送聊天("<我要刷双百>", 0, 1)

自动叠加道具(2, "麻布|20")
自动叠加道具(2, "木棉布|20")
自动叠加道具(2, "毛毡|20")
自动叠加道具(2, "鹿皮|40")
自动叠加道具(1)


标记("造鞋")

//挖鹿皮数
deerskinCount = 200
//制造哪个物品，判断造鞋等级
lvSkill = 获取技能信息("制鞋")
如果( lvSkill == 1 )
	deerskinCount = 400
	nameManufacture = "运动鞋"
如果尾()
如果( lvSkill == 2 )
	deerskinCount = 280
	nameManufacture = "皮鞋"
如果尾()
如果( lvSkill > 2 )
	deerskinCount = 240
	nameManufacture = "光之鞋"
如果尾()

循环( 1 )

	循环(人物详细信息(34) == "艾尔莎岛")
		flag = 0
		如果(curAction == "挖鹿皮")
			flag = 1
			自动寻路(130,50)
			循环(人物详细信息(34) != "盖雷布伦森林")
				等待(100)
			循环尾()
		如果尾()

		如果(flag == 0)
			移动到(140,105)
			循环(人物详细信息(34) != "里谢里雅堡")
				对话NPC(1,7,"","","","")
				等待(100)
			循环尾()
		如果尾()

	循环尾()

	循环(人物详细信息(34) == "盖雷布伦森林")
		flag = 0
		如果(curAction == "挖鹿皮")
			flag = 1
			自动寻路(175,182)

			循环(人物详细信息(4) > 1 && 获取物品数量("鹿皮") < deerskinCount)
				高速采集("狩猎", 5100, "", "", "")
				//如果( 获取信息记录("<回去造鞋>","普通") != "" )
				//	curAction = "造鞋"
				//	跳出循环()
				//如果尾()
			循环尾()

			//扔掉少于1组的鹿皮
			丢弃物品("鹿皮", 40, 1, 1)

			curAction = "回补"
		如果尾()

		如果(flag == 0)
			登出回城()
		如果尾()
	
	循环尾()

	tryMakeCount = 0
	循环(人物详细信息(34) == "里谢里雅堡")
		flag = 0
		如果(curAction == "造鞋" && tryMakeCount < 5)
			flag = 1
			//看是否需要卖鞋
			如果(获取物品数量("运动鞋") > 0 || 获取物品数量("皮鞋") > 0 || 获取物品数量("光之鞋") > 0)
				发送聊天("卖鞋去",4,1)
				//卖鞋去
				自动寻路(41,98)
				循环(人物详细信息(34) != "法兰城")
					等待(100)
				循环尾()
				curAction = "卖鞋"
				跳出循环()
			如果尾()

			//制造哪个物品，判断造鞋等级
			lvSkill = 获取技能信息("制鞋")
			如果(lvSkill == 0 || lvSkill == 5)
				发送聊天("重学技能",4,1)
				转到("重学技能")
			如果尾()
			如果( lvSkill == 1 )
				deerskinCount = 400
				nameManufacture = "运动鞋"
				//看是否需要买布
				如果(获取物品数量("麻布") < 4)
					发送聊天("买布去",4,1)
					//买布去
					自动寻路(17,53)
					循环(人物详细信息(34) != "法兰城")
						等待(100)
					循环尾()
					curAction = "买布"
					跳出循环()
				如果尾()
			如果尾()
			如果( lvSkill == 2 )
				deerskinCount = 280
				nameManufacture = "皮鞋"
				//看是否需要买布
				如果(获取物品数量("麻布") < 4||获取物品数量("木棉布") < 4)
					发送聊天("买布去",4,1)
					//买布去
					自动寻路(17,53)
					循环(人物详细信息(34) != "法兰城")
						等待(100)
					循环尾()
					curAction = "买布"
					跳出循环()
				如果尾()
			如果尾()
			如果( lvSkill > 2 )
				deerskinCount = 240
				nameManufacture = "光之鞋"
				//看是否需要买布
				如果(获取物品数量("麻布") < 5||获取物品数量("木棉布") < 4||获取物品数量("毛毡") < 4)
					发送聊天("买布去",4,1)
					//买布去
					自动寻路(17,53)
					循环(人物详细信息(34) != "法兰城")
						等待(100)
					循环尾()
					curAction = "买布"
					跳出循环()
				如果尾()
			如果尾()

			//方向5是打卡
			自动寻路(59,83)
			
			发送聊天("<给我扔鹿皮>",1,1)
			等待(4000)
			//获取鹿皮
			//捡鹿皮
			deerskinCoord = 获取周围信息("鹿皮","物品")
			循环( deerskinCoord != "" && 获取物品数量("鹿皮") < deerskinCount )
				//发送聊天(deerskinCoord)
				分割文本(deerskinCoords, deerskinCoord, "|")
				//鹿皮地点太远，采用自动寻路()通过否则采用移动到()
				curX = 人物详细信息(35)
				curY = 人物详细信息(36)
				isTooFar = 0
				如果(deerskinCoords.2 >curX+7 || deerskinCoords.2 <curX-7 || deerskinCoords.3 >curY+7 || deerskinCoords.3 <curY-7)
					发送聊天(deerskinCoords.1 $ "(" $ deerskinCoords.2 $ "," $ deerskinCoords.3 $ ")\S太远", 6, 1)
					isTooFar = 1
				如果尾()
				如果(isTooFar==0)
					移动到(deerskinCoords.2,deerskinCoords.3 - 1)
					拾取物品(4)
					等待(1000)
				如果尾()
				如果(isTooFar!=0)
					自动寻路(deerskinCoords.2,deerskinCoords.3 - 1)
					拾取物品(4)
					等待(1000)
				如果尾()

				deerskinCoord = 获取周围信息("鹿皮","物品")
			循环尾()

			自动寻路(59,83)
			发送聊天("<给我捡回去>",1,1)

			//主动交易
			curDeerskinCount = 获取物品数量("鹿皮")
			如果( curDeerskinCount < 1 )
				状态控制(1+32)
				分割文本(result, (deerskinCount - curDeerskinCount + 39) / 40, ".")
				发送聊天("<给我" $ result.1 $ "鹿皮>", 0, 1)
				
				等待(15000)
			如果尾()
			
			如果(获取物品数量("鹿皮") >= 20 && 获取物品数量("麻布") > 3)
				//打卡
				如果(isPunchTheClock != 0)
					对话NPC(5,7,"","","","2")
				如果尾()

				发送聊天("开始制造[" $ nameManufacture $ "]", 1, 1)
				r = 高速制造(nameManufacture, "")
				
				如果(r == 1)
					curAction = "回补"
				如果尾()
				如果(r == 2)
					curAction = "卖鞋"
				如果尾()
				如果(r == 4)
					//curAction = "买布"
				如果尾()

				//取消打卡
				如果(isPunchTheClock != 0)
					对话NPC(5,7,"","","","2")
				如果尾()
				
				tryMakeCount = 0
			如果尾()

			tryMakeCount = tryMakeCount + 1
		如果尾()
		
		如果(tryMakeCount == 5)
			curAction = "挖鹿皮"
		如果尾()

		如果(curAction == "卖鞋")
			flag = 2
			自动寻路(41,98)
			循环(人物详细信息(34) != "法兰城")
				等待(100)
			循环尾()
		如果尾()

		如果(curAction == "买布")
			flag = 3
			自动寻路(17,53)
			循环(人物详细信息(34) != "法兰城")
				等待(100)
			循环尾()
		如果尾()

		如果(curAction == "挖鹿皮")
			flag = 4
			登出回城()
		如果尾()

		如果(curAction == "回补")
			flag = 5
			如果( 人物详细信息(4) < 人物详细信息(5) )
				//先去恢复
				自动寻路(34,89)
				护士恢复(1)
			如果尾()
			curAction = "造鞋"
		如果尾()

		如果(flag == 0)
			curAction = "造鞋"
		如果尾()
	循环尾()

	循环(人物详细信息(34) == "法兰城")
		flag = 0
		如果(curAction == "卖鞋")
			flag = 1
			自动寻路(155,122)
			出售物品(2, "运动鞋|皮鞋|光之鞋")

			自动寻路(153,100)
			循环(人物详细信息(34) != "里谢里雅堡")
				等待(100)
			循环尾()
			curAction = "回补"
		如果尾()

		如果(curAction == "买布")
			flag = 2
			自动寻路(117,112)
			循环(人物详细信息(34) != "流行商店\\z0")
				等待(100)
			循环尾()
		如果尾()

		如果(flag == 0)
			自动寻路(153,100)
			循环(人物详细信息(34) != "里谢里雅堡")
				等待(100)
			循环尾()
			curAction = "造鞋"
		如果尾()

	循环尾()

	循环(人物详细信息(34) == "流行商店\\z0")
		flag = 0
		如果(curAction == "买布")
			flag = 1
			移动到(8,7)

			如果(nameManufacture == "运动鞋")
				//400个鹿皮 10组
				cloth1Count = 获取物品数量("麻布")
				wantCloth1Count = 80
				如果(cloth1Count < wantCloth1Count)
					购买物品(0, "1|" $ (wantCloth1Count - cloth1Count) )
				如果尾()
			如果尾()
			如果(nameManufacture == "皮鞋")
				//280个鹿皮 7组
				cloth1Count = 获取物品数量("麻布")
				wantCloth1Count = 56
				如果(cloth1Count < wantCloth1Count)
					购买物品(0, "1|" $ (wantCloth1Count - cloth1Count) )
				如果尾()
				cloth2Count = 获取物品数量("木棉布")
				wantCloth2Count = 56
				如果(cloth2Count < wantCloth2Count)
					购买物品(0, "2|" $ (wantCloth2Count - cloth2Count))
				如果尾()
			如果尾()
			如果(nameManufacture == "光之鞋")
				//240个鹿皮 6组
				cloth1Count = 获取物品数量("麻布")
				wantCloth1Count = 60
				如果(cloth1Count < wantCloth1Count)
					购买物品(0, "1|" $ (wantCloth1Count - cloth1Count) )
				如果尾()
				cloth2Count = 获取物品数量("木棉布")
				wantCloth2Count = 48
				如果(cloth2Count < wantCloth2Count)
					购买物品(0, "2|" $ (wantCloth2Count - cloth2Count))
				如果尾()
				cloth3Count = 获取物品数量("毛毡")
				wantCloth3Count = 48
				如果(cloth3Count < wantCloth3Count)
					购买物品(0, "3|" $ (wantCloth3Count - cloth3Count))
				如果尾()
			如果尾()

			curAction = "造鞋"
		如果尾()

		如果(flag == 0)
			登出回城()
		如果尾()
	循环尾()

循环尾()



标记("重学技能")
循环( 1 )

	循环(人物详细信息(34) == "艾尔莎岛")
		移动到(140,105)
		循环(人物详细信息(34) != "里谢里雅堡")
			对话NPC(1,7,"","","","")
			等待(100)
		循环尾()
	循环尾()

	循环(人物详细信息(34) == "里谢里雅堡")
		自动寻路(41,50)
		循环(人物详细信息(34) != "里谢里雅堡\S1楼\\z0")
			等待(100)
		循环尾()
	循环尾()

	循环(人物详细信息(34) == "里谢里雅堡\S1楼\\z0")
		自动寻路(45,20)
		循环(人物详细信息(34) != "启程之间\\z0")
			等待(100)
		循环尾()
	循环尾()

	循环(人物详细信息(34) == "启程之间\\z0")
		自动寻路(43,43)
		循环(人物详细信息(34) != "圣拉鲁卡村的传送点\\z0")
			对话NPC(2,7,"","","","")
			等待(100)
		循环尾()
	循环尾()

	循环(人物详细信息(34) == "圣拉鲁卡村的传送点\\z0")
		自动寻路(7,3)
		循环(人物详细信息(34) != "村长的家\\z0")
			等待(100)
		循环尾()
	循环尾()

	循环(人物详细信息(34) == "村长的家\\z0")
		自动寻路(2,9)
		循环(人物详细信息(34) != "圣拉鲁卡村")
			等待(100)
		循环尾()
	循环尾()

	循环(人物详细信息(34) == "圣拉鲁卡村")
		自动寻路(32,70)
		循环(人物详细信息(34) != "装备品店\\z0")
			等待(100)
		循环尾()
	循环尾()

	循环(人物详细信息(34) == "装备品店\\z0")
		自动寻路(14,4)
		循环(人物详细信息(34) != "1楼小房间\\z0")
			等待(100)
		循环尾()
	循环尾()

	循环(人物详细信息(34) == "1楼小房间\\z0")
		自动寻路(9,3)
		循环(人物详细信息(34) != "地下工房\\z0")
			等待(100)
		循环尾()
	循环尾()

	循环(人物详细信息(34) == "地下工房\\z0")
		移动到(23,23)
		
		循环(获取技能信息("制鞋") > 0)
			删除技能(4,"制鞋")
			等待(100)
		循环尾()

		循环(获取技能信息("制鞋") == 0)
			学习技能(4)
			等待(100)
		循环尾()

		登出回城()
		循环(人物详细信息(34) != "艾尔莎岛")
			等待(100)
		循环尾()
		转到("造鞋")

	循环尾()

循环尾()

