// 灵堂小石像怪刷声望
// 请配合【[刷声望]得意技刷声望.conf】配置

curPet = 当前出战宠物()
humanBackHP = 人物详细信息(3)*0.36
humanBackMP = 人物详细信息(5)*0.05
petBackHP = 宠物详细信息(curPet,3)*0.3
petBackMP = 宠物详细信息(curPet,5)*0.05
isAutoSettings = 信息提示("是否以下列数值作为设置{ " $ "人回补血:" $ humanBackHP $ ", " $ "人回补魔:" $ humanBackMP $ ", " $ "宠回补血:" $ petBackHP $ ", " $ "宠回补魔:" $ petBackMP $ " }","36") == 5
如果(isAutoSettings == 0)
    humanBackHP = 信息输入("人回补血值","人回补血值",humanBackHP)
    humanBackMP = 信息输入("人回补魔值","人回补魔值",humanBackMP)
    petBackHP = 信息输入("宠回补血值","宠回补血值",petBackHP)
    petBackMP = 信息输入("宠回补魔值","宠回补魔值",petBackMP)
如果尾()

// 执行动作【刷声望，回补，看声望，取称号】
curAction = 信息输入("执行动作【刷声望，回补，看声望，取称号】","执行动作","刷声望")

battleCount = 0

循环( 1 )

    循环(人物详细信息(34) == "艾尔莎岛")
        //发送聊天("在[" $ 人物详细信息(34) $ "]执行[" $ curAction $ "]...", 4, 1)
        flag = 0
        如果(flag == 0)
            移动到(140,105)
            循环(人物详细信息(34) != "里谢里雅堡")
                对话NPC(1,7,"","","","")
                等待(100)
            循环尾()
        如果尾()

    循环尾()

    循环(人物详细信息(34) == "里谢里雅堡")
        //发送聊天("在[" $ 人物详细信息(34) $ "]执行[" $ curAction $ "]...", 4, 1)
        flag = 0
        如果(curAction == "刷声望")
            flag = 1
            自动寻路(47,85)
            循环(人物详细信息(34) != "召唤之间\\z5")
                等待(100)
            循环尾()
        如果尾()

        如果(curAction == "看声望" || curAction == "取称号")
            flag = 2
            curPet = 当前出战宠物()
            如果(人物详细信息(2) < humanBackHP || 人物详细信息(4) < humanBackMP || 宠物详细信息(curPet,2) < petBackHP || 宠物详细信息(curPet,4) < petBackMP)
                自动寻路(34,89)
                护士恢复(1)
            如果尾()
            自动寻路(54,74)
            对话NPC(2,7,"","","",1)
            
            如果(curAction == "看声望")
                curAction = "刷声望"
            如果尾()
            如果(curAction == "取称号")
                自动寻路(41,98)
                循环(人物详细信息(34) != "法兰城")
                    等待(100)
                循环尾()
            如果尾()
        如果尾()

        如果(flag == 0)
            发送聊天("动作[" $ curAction $ "]在[" $ 人物详细信息(34) $ "]是未定义的...", 6, 1)
        如果尾()
    循环尾()

    循环(人物详细信息(34) == "召唤之间\\z5")
        //发送聊天("在[" $ 人物详细信息(34) $ "]执行[" $ curAction $ "]...", 4, 1)
        flag = 0
        如果(curAction == "刷声望")
            flag = 1
            自动寻路(27,8)
            循环(人物详细信息(34) != "回廊\\z5")
                等待(100)
            循环尾()
        如果尾()

        如果(flag == 0)
            发送聊天("动作[" $ curAction $ "]在[" $ 人物详细信息(34) $ "]是未定义的...", 6, 1)
        如果尾()
    循环尾()

    循环(人物详细信息(34) == "回廊\\z5")
        //发送聊天("在[" $ 人物详细信息(34) $ "]执行[" $ curAction $ "]...", 4, 1)
        flag = 0
        如果(curAction == "刷声望")
            flag = 1
            自动寻路(23,19)
            循环(人物详细信息(34) != "灵堂\\z5")
                等待(100)
            循环尾()
        如果尾()

        如果(curAction == "回补")
            flag = 2
            自动寻路(25,22)
            护士恢复(2)
            curAction = "刷声望"
        如果尾()

        如果(flag == 0)
            发送聊天("动作[" $ curAction $ "]在[" $ 人物详细信息(34) $ "]是未定义的...", 6, 1)
        如果尾()
    循环尾()

    循环(人物详细信息(34) == "灵堂\\z5")
        //发送聊天("在[" $ 人物详细信息(34) $ "]执行[" $ curAction $ "]...", 4, 1)
        flag = 0
        如果(curAction == "刷声望")
            flag = 1
            移动到(29,50)
            循环( 获取信息记录("<回补>","普通") != "" )
                //等待消除全部<回补>
                等待(100)
            循环尾()

            // 自动遇敌开始了
            自动遇敌(1,3,200)
            循环(curAction == "刷声望")
                等待战斗结束()
                curPet = 当前出战宠物()
                如果(人物详细信息(2) < humanBackHP || 人物详细信息(4) < humanBackMP || 宠物详细信息(curPet,2) < petBackHP || 宠物详细信息(curPet,4) < petBackMP)
                    curAction = "回补"
                    跳出循环()
                如果尾()

                如果( 获取信息记录("<回补>","普通") != "" )
                    curAction = "回补"
                    跳出循环()
                如果尾()
                如果( 获取信息记录("<看声望>","普通") != "" )
                    curAction = "看声望"
                    跳出循环()
                如果尾()
                如果( 获取信息记录("<取称号>","普通") != "" )
                    curAction = "取称号"
                    跳出循环()
                如果尾()
            循环尾()
            自动遇敌(0)
            
            battleCount = battleCount + 1
            发送聊天("第" $ battleCount $ "战",4,1)

        如果尾()

        如果(curAction == "回补")
            flag = 2
            自动寻路(31,48)
            循环(人物详细信息(34) != "回廊\\z5")
                等待(100)
            循环尾()

        如果尾()

        如果(curAction == "看声望")
            flag = 3
            登出回城()
            循环(人物详细信息(34) != "艾尔莎岛")
                等待(100)
            循环尾()
            跳出循环()
        如果尾()

        如果(curAction == "取称号")
            flag = 4
            登出回城()
            循环(人物详细信息(34) != "艾尔莎岛")
                等待(100)
            循环尾()
            跳出循环()
        如果尾()

        如果(flag == 0)
            发送聊天("动作[" $ curAction $ "]在[" $ 人物详细信息(34) $ "]是未定义的...", 6, 1)
        如果尾()
    循环尾()

    循环(人物详细信息(34) == "法兰城")
        //发送聊天("在[" $ 人物详细信息(34) $ "]执行[" $ curAction $ "]...", 4, 1)
        flag = 0
        如果(curAction == "取称号")
            flag = 1
            自动寻路(162,130)
            人物转身(2)
            循环(curX != 72 || curY != 123)
                curX = 人物详细信息(35)
                curY = 人物详细信息(36)
            循环尾()
            人物转身(2)
            循环(curX != 233 || curY != 78)
                curX = 人物详细信息(35)
                curY = 人物详细信息(36)
            循环尾()
            自动寻路(230,82)
            人物转身(4)
            
            curAction = "刷声望"
            登出回城()
            循环(人物详细信息(34) != "艾尔莎岛")
                等待(100)
            循环尾()
            跳出循环()

        如果尾()

        如果(flag == 0)
            发送聊天("动作[" $ curAction $ "]在[" $ 人物详细信息(34) $ "]是未定义的...", 6, 1)
        如果尾()
    循环尾()

循环尾()








