// 【Flan脚本】营地练级 - 队长&队员
// 启动点不限，请在辅助面板上设置好回补条件。
// 脚本会自动识别队长，请先组队后再启动脚本。否则会弹出队长名字输入框，那将以面板设置的人数作为队员数。

// 遇敌模式
分割文本(G_EnemyMeetMode, "1,170", ",")
// 练级选择点(地点描述串|默认选择)
分割文本(G_MonsterPlaceChoice,"营地门口:0，沙滩:1|0","|")
// 单人or多人
G_IsSingle = 0
// 卖的物品
G_SaleItems = "魔石|锹型虫的卡片|虎头蜂的卡片|绿色口臭鬼的卡片|水晶怪的卡片|水蜘蛛的卡片|狮鹫的卡片|卡片？"
// 堆叠物品 item1|20,item2|3
G_StackItems = ""
// 丢弃物品 item1,item2
G_ThrowItems = "卡片？,星之砂,秘文之皮,奇香木"

// 如果有堆叠物品，则加入自动堆叠列表
如果(G_StackItems != "")
    自动叠加道具(1)
    分割文本(stackItemsArr,G_StackItems,",")
    nArr = 取成员数(stackItemsArr)
    i = 1
    循环(i <= nArr)
        自动叠加道具(2,stackItemsArr.i)
        i = i + 1
    循环尾()
如果尾()

// 如果有需要丢弃物品，则加入自动丢弃列表
如果(G_ThrowItems != "")
    自动丢弃道具(1)
    分割文本(throwItemsArr,G_ThrowItems,",")
    nArr = 取成员数(throwItemsArr)
    i = 1
    循环(i <= nArr)
        自动丢弃道具(1,throwItemsArr.i)
        i = i + 1
    循环尾()
如果尾()

// 是单人还是多人，并取得队伍人数和队长姓名
如果(G_IsSingle == 1)
    G_PeopleCount = 人物详细信息(37)
    G_TeamMasterName = 人物详细信息(1)
否则()
    获取队长信息(2)
    // 如果人数大于1说明有队伍，则取得队长名字
    如果(人物详细信息(37) > 1)
        G_PeopleCount = 人物详细信息(37)
        G_TeamMasterName = 获取队长信息(1)
    如果尾()

    // 单人队伍从面板获取人数，从面板获取是否队长
    如果(人物详细信息(37) < 2)
        G_PeopleCount = 面板设置信息(7)
        如果(获取队长信息(0) == 5)
            G_TeamMasterName = 信息输入("队长名字","输入队长名字",人物详细信息(1))
        否则()
            G_TeamMasterName = 信息输入("队长名字","输入队长名字",获取队长信息(1))
        如果尾()
    如果尾()
如果尾()

// 如果是队长
如果(人物详细信息(1) == G_TeamMasterName)
    // 是否卖石
    G_IsSaleStone = 信息提示("包满是否出售魔石",36) == 5
    // 是否普通护士
    G_IsCommonNurse = 信息提示("是否去资深护士（即是否不去普通护士）？",36) != 5
    // 练级点
    如果(取成员数(G_MonsterPlaceChoice) > 1)
        G_MonsterPlace = 信息输入(G_MonsterPlaceChoice.1,"选择练级地点",G_MonsterPlaceChoice.2)
    如果尾()
    发送聊天("【Flan脚本】我是队长:[" $ G_TeamMasterName $ "]，队员:[" $ G_PeopleCount $ "]",4,1)
否则()
    发送聊天("【Flan脚本】队长名字:[" $ G_TeamMasterName $ "]，队员:[" $ G_PeopleCount $ "]",2,1)
如果尾()

// 是否去回补
isBack = 0
// 统计次数
battleCount = 0

循环( 1 )
    循环(人物详细信息(34) == "艾尔莎岛")
        如果( isBack == 0 )
            如果(获取队长信息(0) == 5 || 人物详细信息(37) < 2)
                移动到(140,105)
                循环(人物详细信息(34) != "里谢里雅堡")
                    对话NPC(1,2,"","","",2)
                    tryCount = 0
                    循环(tryCount < 40 && 人物详细信息(34) != "里谢里雅堡")
                        等待(100)
                        tryCount = tryCount + 1
                    循环尾()
                循环尾()
            如果尾()
        如果尾()
    循环尾()

    循环(人物详细信息(34) == "里谢里雅堡")
        如果(isBack == 0)
            如果(获取队长信息(0) == 5 || 人物详细信息(37) < 2)
                自动寻路(41,98)
                循环(人物详细信息(34) != "法兰城")
                    等待(100)
                循环尾()
            如果尾()
        如果尾()
    循环尾()

    循环(人物详细信息(34) == "法兰城")
        如果(isBack == 0)
            如果(获取队长信息(0) == 5 || 人物详细信息(37) < 2)
                自动寻路(153,241)
                循环(人物详细信息(34) != "芙蕾雅")
                    等待(100)
                循环尾()
            如果尾()
        如果尾()
    循环尾()

    循环(人物详细信息(34) == "芙蕾雅")
        如果(isBack == 0)
            如果(获取队长信息(0) == 5 || 人物详细信息(37) < 2)
                自动寻路(513,282)
                循环(人物详细信息(34) != "曙光骑士团营地")
                    等待(100)
                循环尾()
            如果尾()
        如果尾()
    循环尾()

    循环(人物详细信息(34) == "曙光骑士团营地")
        如果(isBack == 0)
            如果(获取队长信息(0) == 5 || 人物详细信息(37) < 2)
                移动到(26,59)
                移动到(35,50)
                移动到(52,50)
                自动寻路(55,47)
                循环(人物详细信息(34) != "辛希亚探索指挥部\\z0")
                    等待(100)
                循环尾()
            如果尾()
        如果尾()
    循环尾()

    循环(人物详细信息(34) == "辛希亚探索指挥部\\z0")
        如果(isBack == 0)
            如果(获取队长信息(0) == 5)
                退出队伍()
            如果尾()
            如果(获取队长信息(0) == 5 || 人物详细信息(37) < 2)
                自动寻路(7,4)
                curX = 人物详细信息(35)
                curY = 人物详细信息(36)
                循环(curX != 91 || curY != 6)
                    curX = 人物详细信息(35)
                    curY = 人物详细信息(36)
                循环尾()
                自动寻路(95,9)
                循环(curX != 19 || curY != 28)
                    curX = 人物详细信息(35)
                    curY = 人物详细信息(36)
                循环尾()
                自动寻路(8,22)
                人物转身(7)
                循环(人物详细信息(34) != "圣骑士营地")
                    等待(100)
                循环尾()
            如果尾()
        如果尾()
    循环尾()

    循环(人物详细信息(34) == "圣骑士营地")
        如果(isBack == 0)
            // 队长
            如果(获取队长信息(0) == 5)
                如果(人物详细信息(37) < G_PeopleCount)
                    自动寻路(97,86)
                    切图(7)
                    循环(人物详细信息(37) < G_PeopleCount)
                        等待(1000)
                    循环尾()
                如果尾()

                自动寻路(36,87)
                循环(人物详细信息(34) != "肯吉罗岛")
                    等待(100)
                循环尾()
            如果尾()
            // 队员
            如果(获取队长信息(0) != 5)
                如果(人物详细信息(37) < 2)
                    自动寻路(97,86)
                    tryCount = 0
                    循环( tryCount < 10 && 人物详细信息(37) < 2)
                        加入队伍(7,G_TeamMasterName)
                        等待(500)
                        tryCount = tryCount + 1
                    循环尾()
                如果尾()
            如果尾()
        如果尾()

        如果(isBack != 0)
            如果(获取队长信息(0) == 5)
                如果(G_IsSaleStone == 1)
                    如果(获取物品数量("魔石","") > 0)
                        自动寻路(87,72)
                        循环(人物详细信息(34) != "工房\\z0")
                            等待(100)
                        循环尾()
                    如果尾()
                    如果(获取物品数量("魔石","") < 1)
                        自动寻路(95,72)
                        循环(人物详细信息(34) != "医院\\z0")
                            等待(100)
                        循环尾()
                    如果尾()
                如果尾()

                如果(G_IsSaleStone == 0)
                    自动寻路(95,72)
                    循环(人物详细信息(34) != "医院\\z0")
                        等待(100)
                    循环尾()
                如果尾()
            如果尾()
        如果尾()
    循环尾()

    循环(人物详细信息(34) == "工房\\z0")
        如果( isBack == 0 )
            如果(获取队长信息(0) == 5 || 人物详细信息(37) < 2)
                自动寻路(30,37)
                循环(人物详细信息(34) != "圣骑士营地")
                    等待(100)
                循环尾()
            如果尾()
        如果尾()

        如果( isBack != 0 )
            如果(获取队长信息(0) == 5)
                自动寻路(20,22)
                移动到(20,23)
                移动到(20,22)
                移动到(20,23)
                移动到(20,22)
                移动到(20,23)
                移动到(20,22)

                curX = 人物详细信息(35)
                curY = 人物详细信息(36)
                如果(curX == 20 && curY == 23)
                    出售物品(2,G_SaleItems)
                如果尾()
                如果(curX == 20 && curY == 22)
                    出售物品(3,G_SaleItems)
                如果尾()
                等待(6000)
                自动寻路(30,37)
                循环(人物详细信息(34) != "圣骑士营地")
                    等待(100)
                循环尾()
            如果尾()
        如果尾()

        如果(获取队长信息(0) != 5)
            curX = 人物详细信息(35)
            curY = 人物详细信息(36)
            如果(curX == 20 && curY == 23)
                出售物品(2,G_SaleItems)
            如果尾()
            如果(curX == 20 && curY == 22)
                出售物品(3,G_SaleItems)
            如果尾()
        如果尾()
    循环尾()

    循环(人物详细信息(34) == "肯吉罗岛")
        如果( isBack == 0 )
            如果(获取队长信息(0) == 5)
                如果(G_MonsterPlace == 0)
                    自动寻路(545,328)
                    // 自动遇敌开始了
                    自动遇敌(1,G_EnemyMeetMode.1,G_EnemyMeetMode.2)
                    循环(是否需要回补() == 0 && (G_IsSaleStone == 0 || 人物详细信息(33) < 20))
                        等待(1000)
                        等待战斗结束()
                    循环尾()
                    自动遇敌(0)
                    isBack = 1
                如果尾()
                如果(G_MonsterPlace == 1)
                    自动寻路(472,214)
                    // 自动遇敌开始了
                    自动遇敌(1,G_EnemyMeetMode.1,G_EnemyMeetMode.2)
                    循环(是否需要回补() == 0 && (G_IsSaleStone == 0 || 人物详细信息(33) < 20))
                        等待(1000)
                        等待战斗结束()
                    循环尾()
                    自动遇敌(0)
                    isBack = 1
                如果尾()
                如果(G_MonsterPlace == 2)
                    自动寻路(384,245)
                    tryCount = 0
                    循环(tryCount < 40 && 人物详细信息(34) != "蜥蜴洞穴\\z3")
                        等待(100)
                        tryCount = tryCount + 1
                    循环尾()
                    自动寻路(12,2)
                    tryCount = 0
                    循环(tryCount < 40 && 人物详细信息(34) != "肯吉罗岛")
                        等待(100)
                        tryCount = tryCount + 1
                    循环尾()

                    自动寻路(234,439)
                    // 自动遇敌开始了
                    自动遇敌(1,G_EnemyMeetMode.1,G_EnemyMeetMode.2)
                    循环(是否需要回补() == 0 && (G_IsSaleStone == 0 || 人物详细信息(33) < 20))
                        等待(1000)
                        等待战斗结束()
                    循环尾()
                    自动遇敌(0)
                    isBack = 1
                如果尾()
            如果尾()

            如果(获取队长信息(0) != 5)
                等待(1000)
                等待战斗结束()
            如果尾()
        如果尾()

        如果(isBack != 0)
            如果(获取队长信息(0) == 5)
                如果(G_MonsterPlace < 2)
                    自动寻路(551,332)
                    循环(人物详细信息(34) != "圣骑士营地")
                        等待(100)
                    循环尾()
                如果尾()
                如果(G_MonsterPlace == 2)
                    自动寻路(231,434)
                    tryCount = 0
                    循环(tryCount < 40 && 人物详细信息(34) != "矮人城镇")
                        等待(100)
                        tryCount = tryCount + 1
                    循环尾()
                如果尾()
            如果尾()
        如果尾()
    循环尾()

    循环(人物详细信息(34) == "矮人城镇")
    //卖石122,110
    //资深163,95,2
    //护士163,94,2
    循环尾()

    循环(人物详细信息(34) == "医院\\z0")
        如果(isBack == 0)
            如果(获取队长信息(0) == 5 || 人物详细信息(37) < 2)
                自动寻路(5,20)
                移动到(0,20)
                循环(人物详细信息(34) != "圣骑士营地")
                    等待(100)
                循环尾()
            如果尾()
        如果尾()

        如果(isBack != 0)
            如果(获取队长信息(0) == 5)
                移动到(5,20)
                如果(G_IsCommonNurse == 0)
                    移动到(9,16)
                    移动到(9,10)
                    移动到(9,12)
                    等待(2000)
                    护士恢复(1)
                如果尾()
                如果(G_IsCommonNurse != 0)
                    自动寻路(17,17)
                    //移动到(17,13)
                    移动到(17,15)
                    //移动到(17,13)
                    等待(2000)
                    护士恢复(1)
                如果尾()
                isBack = 0
                battleCount = battleCount + 1
                发送聊天("【Flan脚本】营地练级已经执行["$ battleCount $"]次。",8,1)
            如果尾()
        如果尾()
    循环尾()

循环尾()








