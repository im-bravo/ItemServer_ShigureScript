// 高地杀鸡、龙骨、黄金龙骨 - 队长版

//nPeople = 信息输入("队伍人数","输入队伍成员数(0~5)","5")
//isSaleStone = 信息提示("包满是否出售魔石","36")
curPet = 当前出战宠物()

monster = 信息输入("刀鸡:0，龙骨:1，黄金龙骨:2","怪物","0")
humanBackHP = 人物详细信息(3)*0.5
humanBackMP = 人物详细信息(5)*0.05
petBackHP = 宠物详细信息(curPet,3)*0.4
petBackMP = 宠物详细信息(curPet,5)*0.05
isAutoSettings = 信息提示("是否以下列数值作为设置{ " $ "人回补血:" $ humanBackHP $ ", " $ "人回补魔:" $ humanBackMP $ ", " $ "宠回补血:" $ petBackHP $ ", " $ "宠回补魔:" $ petBackMP $ " }","36")
如果(isAutoSettings != 5)
	humanBackHP = 信息输入("人回补血值","人回补血值",humanBackHP)
	humanBackMP = 信息输入("人回补魔值","人回补魔值",humanBackMP)
	petBackHP = 信息输入("宠回补血值","宠回补血值",petBackHP)
	petBackMP = 信息输入("宠回补魔值","宠回补魔值",petBackMP)
如果尾()

isBack = 0

循环( 1 )

	循环(人物详细信息(34) == "艾尔莎岛")
		移动到(157,94)
		人物转身(1)
		循环(人物详细信息(34) != "艾夏岛")
			等待(100)
		循环尾()
	循环尾()

	循环(人物详细信息(34) == "艾夏岛")
		如果( isBack == 0 )
			curX = 人物详细信息(35)
			curY = 人物详细信息(36)
			flag = 0
			如果(curX >= 81 && curX <= 86 && curY >= 108 && curY <= 117) // 在传送石1范围
				flag = flag + 1
				移动到(84,112)
				人物转身(6)
				循环(curX != 164 || curY != 159)
					curX = 人物详细信息(35)
					curY = 人物详细信息(36)
				循环尾()
				//发送聊天(人物详细信息(35) $ "," $ 人物详细信息(36),0)
			如果尾()
			如果(curX >= 159 && curX <= 165 && curY >= 154 && curY <= 160) // 在传送石2范围
				flag = flag + 1
				移动到(164,159)
				人物转身(7)
				循环(curX != 151 || curY != 97)
					curX = 人物详细信息(35)
					curY = 人物详细信息(36)
				循环尾()
				//发送聊天(人物详细信息(35) $ "," $ 人物详细信息(36),1)
			如果尾()
			如果(curX >= 145 && curX <= 156 && curY >= 92 && curY <= 105) // 在传送石3范围
				flag = flag + 1
				//移动到(151,97)
				自动寻路(190,116)
				循环(人物详细信息(34) != "盖雷布伦森林")
					等待(100)
				循环尾()
				//发送聊天(人物详细信息(35) $ "," $ 人物详细信息(36),2)
			如果尾()

			//发送聊天(flag,3)
			如果( flag == 0 )
				自动寻路(190,116)
				循环(人物详细信息(34) != "盖雷布伦森林")
					等待(100)
				循环尾()
			如果尾()
		如果尾()

		如果(isBack != 0)
			自动寻路(112,81)
			循环(人物详细信息(34) != "医院\\z0")
				等待(100)
			循环尾()
		如果尾()

	循环尾()

	循环(人物详细信息(34) == "盖雷布伦森林")
		如果( isBack == 0 )
			自动寻路(231,222)
			循环(人物详细信息(34) != "布拉基姆高地")
				等待(100)
			循环尾()
		如果尾()

		如果(isBack != 0)
			自动寻路(199,211)
			循环(人物详细信息(34) != "艾夏岛")
				等待(100)
			循环尾()
		如果尾()
	循环尾()

	循环(人物详细信息(34) == "布拉基姆高地")
		如果( isBack == 0 )
			如果(monster == 0)
				自动寻路(37,187)
			如果尾()
			如果(monster == 1)
				自动寻路(115,203)
			如果尾()
			如果(monster == 2)
				自动寻路(135,175)
			如果尾()

			// 自动遇敌开始了
			自动遇敌(1,3,170)
			循环( 获取信息记录("<back>","普通") != "" )
				//等待消除全部<back>
				等待(100)
			循环尾()
			循环(isBack == 0)
				等待战斗结束()
				curPet = 当前出战宠物()
				如果(人物详细信息(2) < humanBackHP)
					isBack = 1
					跳出循环()
				如果尾()
				如果(人物详细信息(4) < humanBackMP)
					isBack = 1
					跳出循环()
				如果尾()
				如果(宠物详细信息(curPet,2) < petBackHP)
					isBack = 1
					跳出循环()
				如果尾()
				如果(宠物详细信息(curPet,4) < petBackMP)
					isBack = 1
					跳出循环()
				如果尾()
				如果( 获取信息记录("<back>","普通") != "" )
					isBack = 1
					跳出循环()
				如果尾()

			循环尾()

			自动遇敌(0)
		如果尾()

		如果(isBack != 0)
			自动寻路(30,193)
			循环(人物详细信息(34) != "盖雷布伦森林")
				等待(100)
			循环尾()
		如果尾()
	循环尾()

	循环(人物详细信息(34) == "医院\\z0")
		如果(isBack == 0)
			自动寻路(28,52)
			循环(人物详细信息(34) != "艾夏岛")
				等待(100)
			循环尾()
		如果尾()

		如果(isBack != 0)
			移动到(35,45)
			移动到(35,47)
			移动到(35,45)
			移动到(35,46)
			护士恢复(2)
			isBack = 0
			等待(3000)
			循环( 获取信息记录("<back>","普通") != "" )
				//等待消除全部<back>
				等待(100)
			循环尾()
		如果尾()
	循环尾()

循环尾()








