// 【Flan脚本】高地杀鸡、龙骨、黄金龙骨 - 队长&队员版
// 启动点不限，请在辅助面板上设置好回补条件。
// 脚本会自动识别队长，请先组队后再启动脚本。否则会弹出队长名字输入框，那将以面板设置的人数作为队员数。

// 遇敌模式
分割文本(enemyMeetMode, "1,170", ",")

// 如果人数大于1说明有队伍，则取得队长名字
如果(人物详细信息(37) > 1)
    nPeople = 人物详细信息(37)
    获取队长信息(2)
    teamMaster = 获取队长信息(1)
如果尾()

// 单人队伍从面板获取人数
如果(人物详细信息(37) < 2)
    nPeople = 面板设置信息(7)
    teamMaster = 信息输入("队长名字","输入队长名字",人物详细信息(1))
如果尾()

如果(人物详细信息(1) == teamMaster)
    // 卖的物品
    //saleItems = "魔石|卡片？"
    // 是否卖石
    //isSaleStone = 信息提示("包满是否出售魔石",36) == 5
    // 是否普通护士
    isCommonNurse = 信息提示("是否不去普通护士？",36) != 5
    // 练级点
    monster = 信息输入("刀鸡:0，龙骨:1，黄金龙骨:2","选择怪物","0")
    发送聊天("我是队长:[" $ teamMaster $ "]，队员:[" $ nPeople $ "]...",4,1)
否则()
    发送聊天("队长名字:[" $ teamMaster $ "]，队员:[" $ nPeople $ "]...",2,1)
如果尾()

isBack = 0
// 统计次数
battleCount = 0

循环( 1 )
    循环(人物详细信息(34) == "艾尔莎岛")
        如果(获取队长信息(0) == 5)
            移动到(157,94)
            人物转身(1)
            循环(人物详细信息(34) != "艾夏岛")
                等待(100)
            循环尾()
        如果尾()
    循环尾()

    循环(人物详细信息(34) == "艾夏岛")
        如果( isBack == 0 )
            如果(获取队长信息(0) == 5)
                curX = 人物详细信息(35)
                curY = 人物详细信息(36)
                flag = 0
                如果(curX >= 81 && curX <= 86 && curY >= 108 && curY <= 117) // 在传送石1范围
                    flag = flag + 1
                    移动到(84,112)
                    人物转身(6)
                    循环(curX != 164 || curY != 159)
                        curX = 人物详细信息(35)
                        curY = 人物详细信息(36)
                    循环尾()
                    //发送聊天(人物详细信息(35) $ "," $ 人物详细信息(36),0)
                如果尾()

                如果(curX >= 159 && curX <= 165 && curY >= 154 && curY <= 160) // 在传送石2范围
                    flag = flag + 1
                    移动到(164,159)
                    人物转身(7)
                    循环(curX != 151 || curY != 97)
                        curX = 人物详细信息(35)
                        curY = 人物详细信息(36)
                    循环尾()
                    //发送聊天(人物详细信息(35) $ "," $ 人物详细信息(36),1)
                如果尾()

                如果(curX >= 145 && curX <= 156 && curY >= 92 && curY <= 105) // 在传送石3范围
                    flag = flag + 1
                    //移动到(151,97)
                    自动寻路(190,116)
                    循环(人物详细信息(34) != "盖雷布伦森林")
                        等待(100)
                    循环尾()
                    //发送聊天(人物详细信息(35) $ "," $ 人物详细信息(36),2)
                如果尾()

                //发送聊天(flag,3)
                如果( flag == 0 )
                    自动寻路(190,116)
                    循环(人物详细信息(34) != "盖雷布伦森林")
                        等待(100)
                    循环尾()
                如果尾()
            如果尾()
        如果尾()

        如果(isBack != 0)
            如果(获取队长信息(0) == 5)
                自动寻路(112,81)
                循环(人物详细信息(34) != "医院\\z0")
                    等待(100)
                循环尾()
            如果尾()
        如果尾()

    循环尾()

    循环(人物详细信息(34) == "盖雷布伦森林")
        如果( isBack == 0 )
            如果(获取队长信息(0) == 5)
                自动寻路(231,222)
                循环(人物详细信息(34) != "布拉基姆高地")
                    等待(100)
                循环尾()
            如果尾()
        如果尾()

        如果(isBack != 0)
            如果(获取队长信息(0) == 5)
                自动寻路(199,211)
                循环(人物详细信息(34) != "艾夏岛")
                    等待(100)
                循环尾()
            如果尾()
        如果尾()
    循环尾()

    循环(人物详细信息(34) == "布拉基姆高地")
        如果( isBack == 0 )
            如果(获取队长信息(0) == 5)
                如果(monster == 0)
                    自动寻路(37,187)
                如果尾()
                如果(monster == 1)
                    自动寻路(115,203)
                如果尾()
                如果(monster == 2)
                    自动寻路(135,174)
                如果尾()

                // 自动遇敌开始了
                自动遇敌(1,enemyMeetMode.1,enemyMeetMode.2)

                循环(是否需要回补() == 0)
                    等待(1000)
                    等待战斗结束()
                循环尾()

                自动遇敌(0)
                isBack = 1
            如果尾()

            如果(获取队长信息(0) != 5)
                等待(1000)
                等待战斗结束()
            如果尾()
        如果尾()

        如果(isBack != 0)
            如果(获取队长信息(0) == 5)
                自动寻路(30,193)
                循环(人物详细信息(34) != "盖雷布伦森林")
                    等待(100)
                循环尾()
            如果尾()
        如果尾()
    循环尾()

    循环(人物详细信息(34) == "医院\\z0")
        如果(isBack == 0)
            如果(获取队长信息(0) == 5)
                自动寻路(28,52)
                循环(人物详细信息(34) != "艾夏岛")
                    等待(100)
                循环尾()
            如果尾()
        如果尾()

        如果(isBack != 0)
            如果(获取队长信息(0) == 5)
                如果(isCommonNurse == 0)
                    移动到(35,42)
                    移动到(35,44)
                    移动到(35,42)
                    移动到(35,43)
                    护士恢复(2)
                如果尾()
                如果(isCommonNurse != 0)
                    移动到(35,45)
                    移动到(35,47)
                    移动到(35,45)
                    移动到(35,46)
                    护士恢复(2)
                如果尾()
                等待(3000)
                isBack = 0
                battleCount = battleCount + 1
                发送聊天("【Flan脚本】高地练级已经执行["$ battleCount $"]次。",8,1)
            如果尾()
        如果尾()
    循环尾()

循环尾()








